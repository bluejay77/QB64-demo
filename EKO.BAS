10 REM *** EKOSYSTEEMIA SIMULOIVA OHELMA ***
12 REM ANTTI J. YLIKOSKI 05-08-2007
20 REM OHJELMA SIMULOI N PETOA JA M SAALISELAINTA KASITTAVAA
30 REM EKOSYSTEEMIA.  SE LASKEE ELAINTEN MAARAN VUODEN VALEIN,
40 REM KUNNES ELAIMET OVAT KUOLLEET SUKUPUUTTOON, VAIN SAALIS-
50 REM ELAIMIA ON JALJELLA TAI V VUOTTA ON KULUNUT.
60 READ A, B, C, D, M, N, V
65 LOG10 = LOG(10)
70 DEF FNL (X) = LOG(X) / LOG10: REM 10-KANTAINEN LOGARITMI
80 PRINT
90 PRINT "ELAINTEN LUKUMAARA: P=PEDOT, S=SAALIIT"
100 FOR L% = 0 TO 4
110 PRINT TAB(10 * L%); 10 ^ L%;
120 NEXT L%
130 PRINT TAB(50); " 100000"; TAB(60); " 1000000"
140 FOR L% = 0 TO 6
150 PRINT TAB(10 * L%); "I";
160 NEXT L%
170 PRINT
180 FOR L% = 0 TO V
190 IF M AND N THEN 290
200 IF M THEN 240
210 IF N THEN 270
220 PRINT "++"
230 GOTO 440
240 PRINT "+"; TAB(10 * FNL(M)); "S"
250 IF M > 1E+07 THEN 440
260 GOTO 340
270 PRINT "+"; TAB(10 * FNL(N)); "P"
280 GOTO 340
290 IF N > M THEN 320
300 PRINT TAB(10 * FNL(N)); "P"; TAB(10 * FNL(M)); "S"
310 GOTO 340
320 PRINT TAB(10 * FNL(M)); "S"; TAB(10 * FNL(N)); "P"
325 REM DIFFERENSSIYHTALON SIMULOINTI, 10 AIKAVALIA
340 FOR K% = 1 TO 10
350 LET M = M + .1 * (A * M - B * M * N)
360 IF M >= 1 THEN 380
370 LET M = 0
380 LET N = N + .1 * (C * M * N - D * N)
390 IF N >= 1 THEN 410
400 LET N = 0
410 NEXT K%
420 NEXT L%
430 DATA 0.7, 0.004, 0.002, 2.0, 1000, 100, 20
440 END

