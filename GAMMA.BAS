10 REM CALCULATING THE GAMMA FUNCTION
20 DEFDBL A-Z: REM ALL VARIABLES ARE DOUBLE PRECISION
40 CLS
50 REM CALCULATE THE GAMMA FUNCTION WITH
60 REM NUMERICAL INTEGRATION
70 REM AND COMPARE IT WITH THE FACTORIAL
1000 REM THE GAMMA FUNCTION
1010 REM GAMMA(X) = INTEGRAL [0.0 TO INF] EXP(-T) * T ^(X-1.0) DT
1020 DEF FNF (T) = EXP(-T) * T ^ (X - 1!)
1030 REM
1040 CLS
1050 INPUT "ARGUMENT OF GAMMA?", X
1060 LET A = 0!
1070 LET B = 50!
1080 LET DELTA = .01
1090 GOSUB 3000
1100 PRINT "GAMMA("; X; ") = "; SIMPSON
1110 LET N = INT(X - 1)
1120 GOSUB 5000
1130 PRINT "FACTORIAL("; N; ") = "; FACT
1140 END
3000 REM THE SIMPSON RULE FOR INTEGRATION
3010 REM INTEGRAL [A TO B] = (B - A) / 6.0 *
3015 REM                       (F(A) +
3020 REM                        4.0 * F((A + B) / 2) +
3030 REM                        F(B))
3040 REM REPEATED USE OF THE SIMPSON RULE
3050 REM THE FUNCTION TO BE INTEGRATED = FNF(X)
3060 REM INPUT VARIABLES: A AND B, AND, DELTA
3065 REM OUTPUT VARIABLE: SIMPSON
3070 LET TIMES = (B - A) / DELTA
3075 LET SIMPSON = 0!
3080 FOR T = A TO B STEP DELTA
3090 LET TEMP = FNF(T) + 4! * FNF((T + DELTA / 2!)) + FNF(T + DELTA)
3100 LET SIMP = DELTA / 6! * TEMP
3110 LET SIMPSON = SIMPSON + SIMP
3120 NEXT T
3130 RETURN
5000 REM FACTORIAL(N)
5010 REM INPUT VARIABLE: N
5020 REM OUTPUT VARIABLE: FACT
5030 LET FACT = 1
5040 FOR K = 1 TO N
5050 LET FACT = FACT * K
5060 NEXT K
5070 RETURN
9999 END

