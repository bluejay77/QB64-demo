1000 REM ***** GAUSSIN NORMAALIJAKAUMAN KERTYMAFUNKTIO *****
1010 REM ***** FOR THE FIRST TIME WRITTEN IN THE S-OS, TKK *****
1020 REM ***** OHJELMA LASKEE GAUSSIN ELI NORMAALIJAKAUMAN
1030 REM ***** KERTYMAFUNKTION ARVOJA SARJAKEHITELMALLA
1032 DEFDBL A-Z: REM ALL VARIABLES ARE DOUBLE PRECISION
1033 LET PI4 = ATN(1#): REM ARCUS TANGENT 1 = PI / 4
1035 LET PI = 4# * PI4
1037 LET COEFF = SQR(1# / (2# * PI))
1040 REM
1050 REM
1051 LET LIMIT = 6!
1052 REM ESIMERKKI SARJAKEHITELMALLA LASKETTAVASTA FUNKTIOSTA
1053 REM GAUSSIN KELLOKAYRAFUNKTIO VOIDAAN TERMEITTAIN INTEGROIDA JA
1054 REM LASKEA ALTERNOIVANA SARJANA
1055 CLS
1060 PRINT "LASKEMME NORMAALIJAKAUMAN KERTYMAFUNKTION ARVOJA."
1070 PRINT "ANNA ARGUMENTTI: ";
1080 INPUT X
1090 REM X ON FUNKTION ARGUMENTTI, A TERMIEN LASKEMISESSA TARVITTAVA
1100 REM APUMUUTTUJA, T SARJAN N:S TERMI JA S SARJAN N:S OSASUMMA
1110 REM KOSKA TIETOKONE KASITTELEE LUKUJA VAIN TIETYLLA TARKKUUDELLA,
1120 REM TARKKUUS HEIKKENEE KUN ABS(X) > LIMIT (KOKEELLISESTI TODETTU)
1130 IF ABS(X) <= LIMIT THEN 1160
1140 PRINT "ARGUMENTIN ITSEISARVO ON VALITETTAVASTI LIIAN SUURI."
1150 GOTO 1070
1160 LET S = X
1165 LET A = X
1170 LET N = 0
1180 LET N = N + 1: REM SARJAN N'S TERMI KASITELTAVANA
1190 LET A = A * X * X / N / (-2!)
1200 LET T = A / (2! * N + 1!): REM T ON SARJAN N'S TERMI
1210 IF ABS(T) < 9.999999999999999D-21 THEN 1240
1220 LET S = S + T: REM SUMMATAAN N'S TERMI OSASUMMAAN S
1230 GOTO 1180
1240 REM NOW THE SERIES HAS BEEN SUMMED WITH THE DESIRED PRECISION
1245 LET S = .5 + COEFF * S
1250 PRINT "KERTYMAFUNKTIO : "; S
1270 PRINT "JATKAMMEKO (K/E) ";
1280 INPUT A$
1290 IF A$ = "K" OR A$ = "k" THEN 1070
1300 END

