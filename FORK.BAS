2 REM ***** OHJELMA JOHDETTU OTADATAN KIRJASTA:
3 REM ***** PERUSTIETOJA TIETOJENKASITTELYSTA, ISBN 951-767-023-0 *****
4 REM ***** AUTOMAATTINEN HAARUKOINTI
6 REM ***** FUNKTION NOLLAKOHDAN ETSIMINEN PUOLITTAMISMENETTELYLLA
7 REM ***** EDITED AND DEBUGGED BY A. J. YLIKOSKI
8 REM ***** (THE OTADATA PROGRAM DID NOT RUN)
10 DATA 1, 2, .000001
15 CLS
20 PRINT "AUTOMAATTINEN HAARUKOINTI"
30 READ A, B, F
32 REM A ON VALIN AJARAJA, B SEN YLARAJA, F NOLLAKOHDAN TARKKUUS
40 DEF FNA (X) = X * X * X - X * X + 3! * X - 4
42 REM THE ZERO OF THE ABOVE FUNCTION IS FOUND
50 LET MLO = SGN(FNA(A)): REM FUNKTION MERKKI INFIMUM'ISSA
52 LET MHI = SGN(FNA(B)): REM FUNKTION MERKKI SUPREMUM'ISSA
54 IF MLO <> MHI THEN 60
56 PRINT "FUNKTIO SAMANMERKKINEN INTERVALLIN MOLEMMISSA PAISSA: VIRHE!"
58 END
60 LET C = A / 2! + B / 2!: REM ITSE ASIASSA C = (A + B) / 2
70 LET V = FNA(C)
80 IF ABS(V) <= F THEN 150
90 LET M = SGN(V): REM FUNKTION MERKKI KESKIPISTEESSA
100 IF M = MLO THEN 130: REM SAMANMERKKINEN KUIN INFIMUM'ISSA??
110 LET B = C: REM ASETETAAN INTERVALLIN SUPREMUM
120 GOTO 60
130 LET A = C: REM ASETETAAN INTERVALLIN INFIMUM
140 GOTO 60
150 PRINT "JUURI ON "; C; "FUNKTION ARVO TALLOIN ON "; V
160 END


