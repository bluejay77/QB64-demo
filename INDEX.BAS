1 REM *** TALOUSELAMAN SIMULOINTI ***
2 REM PERUSMUODOSSA NAHDAAN HINTOJEN JA PALKKOJEN KILPAJUOKSU
3 REM ANTTI YLIKOSKI 20. 4. 2007
4 REM OHJELMA ON ALUNPERIN VTKK:STA
6 REM ANTTI YLIKOSKI 2016-12-22 SKROLLI-LEHDELLE
10 P1 = 100: I1 = 100: H1 = 100: REM INDEKSIT ALKAVAT 100:STA
15 READ LKM: REM SIMULOITAVIEN KIERROSTEN MAARA
20 P2 = 100: REM MUUTTUJIEN ALKUARVOT
30 I2 = 103
40 H2 = 108
45 CLS: REM TYHJENNA KUVARUUTU
50 PRINT "KUUKAUSI", "PALKAT", "HINNAT", "INDEKSI"
60 T = 1: REM 1. KIERROS
70 PRINT T, INT(P1), INT(H1), INT(I1)
80 T = 2
90 PRINT T, INT(P2), INT(H2), INT(I2): REM SILMUKAN ALKUKOHTA
100 IF I2 / I1 > 1.02 THEN 130 : REM INDEKSI NOUSSUT YLI 2%?
110 P3 = P2 : REM MUUTOIN EI KOROTETA PALKKOJA
120 GOTO 140
122 REM PALKAT NOUSEVAT INDEKSIEN SUHTEESSA, RIVI 130
123 REM - TAHAN VOISI MYOS OHJELMOIDA PALKKASULUN
124 REM - TAHAN VOISI MYOSKIN LAITTAA PALKKOJEN NOUSUN INDEKSI-
125 REM SIDONNAISUUDEN VAPAUTUKSEN
130 P3 = P2 * (I2 / I1)
132 REM TALOUSELAMA SIIRTAA PALKKOJEN NOUSUN SUORAAN HINTOIHIN, RIVI 140
133 REM TASSA VOITAISIIN SIMULOIDA TUOTTAVUUDEN NOUSUA (UUSI
134 REM TEKNOLOGIA, RATIONALISOINTI)
135 REM SITEN, ETTA PALKAT NOUSEVAT MUTTA HINNAT
136 REM NOUSEVAT VAHEMMAN
137 REM TAHAN VOISI MYOS OHJELMOIDA HINTASULUN
140 H3 = H2 + (P2 - P1)
150 I3 = I2 * (H2 / H1): REM INDEKSIN NOUSU LASKETAAN HINNOISTA
160 P1 = P2: REM PAIVITETAAN MUUTTUJAT SEURAAVALLE
170 P2 = P3: REM ITERAATIOKIERROKSELLE
180 H1 = H2
190 H2 = H3
200 I1 = I2
210 I2 = I3
220 T = T + 1
230 IF T <= LKM THEN 90: REM KIERROSMAARA TAYSI?
235 DATA 12
240 END

