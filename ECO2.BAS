' *** AN ECOSYSTEM SIMULATION ***
' BASED ON THE LOTKA-VOLTERRA EQUATIONS (SEE WIKIPEDIA)
' ANTTI YLIKOSKI 05-08-2007
' THIS VERSION FOR THE QB64 BY AJY 2013-03-03
' ANTTI YLIKOSKI 2017-02-16 MIKROBITTI-LEHDELLE
' THE PROGRAM SIMULATES AN ECOSYSTEM WITH M PREY ANIMALS
' AND N PREDATORS.
' THE PROGRAM CALCLULATES THE NUMBER OF ORGANISMS EVERY YEAR,
' UNTIL ALL ORGANISMS ARE EXTINCT, ONLY PREY ORGANISMS REMAIN, OR
' V YEARS HAVE ELAPSED.
READ A, B, C, D, M, N, V
PRINT "THE NUMBER OF ORGANISMS: P=PREDATORS, A=PREY ANIMALS"
PRINT "TIME INCREASES DOWNWARDS, EACH LINE IS ONE TIME UNIT ELAPSED"
PRINT "WATCH THE TYPICAL FLUCTUATIONS OF THE POPULATIONS UNFOLD"
FOR L% = 0 TO 4 ' LOGARITHMIC COORDINATE AXIS
    PRINT TAB(10 * L%); 10 ^ L%;
NEXT L%
PRINT TAB(50); " 100000"; TAB(60); " 1000000"
FOR L% = 0 TO 6
    PRINT TAB(10 * L%); "I";
NEXT L%
PRINT
FOR L% = 0 TO V
    IF (M = 0) AND (N = 0) THEN ' BOTH POPULATIONS ARE DEAD?
        PRINT "++"
        END
    ELSEIF (M > 0) AND (N = 0) THEN
        PRINT "+"; TAB(INT(10 * FNL(M))); "A" ' ONLY PREY ONES REMAIN
        IF M > 1E+07 THEN END
    ELSEIF (M = 0) AND (N > 0) THEN
        PRINT "+"; TAB(INT(10 * FNL(N))); "P" ' ONLY PREDATORS REMAIN
    ELSEIF (M > N) THEN ' DRAW BOTH MARKERS, "P" AND "A"
        PRINT TAB(INT(10 * FNL(N))); "P"; TAB(INT(10 * FNL(M))); "A"
    ELSE
        PRINT TAB(INT(10 * FNL(M))); "A"; TAB(INT(10 * FNL(N))); "P"
    END IF
    ' SIMULATE A GROUP OF TWO DIFFERENTIAL EQUATIONS WITH
    ' TWO DIFFERENCE EQUATIONS, 10 TIME INTERVALS
    FOR K% = 1 TO 10
        LET M = M + .1 * (A * M - B * M * N)
        IF (M < 1) THEN M = 0 ' ALL THE PREY ONES ARE DEAD
        LET N = N + .1 * (C * M * N - D * N)
        IF (N < 1) THEN N = 0 ' ALL THE PREDATORS ARE DEAD
    NEXT K%
    _DELAY 1 ' QB64: PAUSE FOR ONE SECOND
NEXT L%
DATA 0.7,0.004,0.002,2.0,1000,100,17
END
' THE PARAMETER VALUES GIVEN ABOVE GIVE THE CHARACTERISTIC
' FLUCTUATIONS IN THE PREY AND PREDATOR POPULATIONS THAT WILL
' BE OBSERVED IN THE NATURE.
' EXPERIMENT WITH TWEAKING THE VALUES OF A, B, C, AND D.

FUNCTION FNL (X): ' BASE TEN LOGARITHM
FNL = LOG(X) / LOG(10)
END FUNCTION

