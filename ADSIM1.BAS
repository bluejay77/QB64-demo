' THE ADAPTIVE SIMPSON INTEGRAL
'
' ANTTI YLIKOSKI 2017-03-09 MIKROBITTI-LEHDELLE
'



PRINT "ADAPTIVE SIMPSON INTEGRAL = ", ADSIM1(1.0, 2.0, 0.0000000001)
PRINT "THE EXACT VALUE LN(2)     = ", LOG(2.0)
PRINT "THE VALUE OF EPSILON      = ", 0.0000000001

END


FUNCTION F (X) ' THE FUNCTION TO BE INTEGRATED = F()
F = 1 / X
END FUNCTION


FUNCTION SIMPSON1 (AB, BB)

DIM C AS DOUBLE
DIM H3 AS DOUBLE

C = (AB + BB) / 2.0
H3 = ABS(BB - AB) / 6.0
SIMPSON1 = H3 * (F(AB) + 4.0 * F(C) + F(BB))
END FUNCTION


FUNCTION REC_ASR (AB, BB, EPS, W)

DIM C AS DOUBLE

C = (AB + BB) / 2.0

DIM LEFT AS DOUBLE
DIM RIGHT AS DOUBLE

LEFT = SIMPSON1(AB, C)
RIGHT = SIMPSON1(C, BB)

IF (ABS(LEFT + RIGHT - W) <= 15.0 * EPS) THEN
    REC_ASR = LEFT + RIGHT + (LEFT + RIGHT - W) / 15.0
    EXIT FUNCTION
END IF

REC_ASR = REC_ASR(AB, C, EPS / 2.0, LEFT) + REC_ASR(C, BB, EPS / 2.0, RIGHT)

END FUNCTION


FUNCTION ADSIM1 (AB, BB, EPS)
ADSIM1 = REC_ASR(AB, BB, EPS, SIMPSON1(AB, BB))
END FUNCTION


